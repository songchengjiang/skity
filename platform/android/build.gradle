plugins {
    alias(libs.plugins.android.library)
}

android {
    namespace 'com.lynxsdk.lynx.skity'
    compileSdk 35

    def osName = System.properties['os.name'].toLowerCase()
    if (osName.contains('mac')) {
        ndkVersion = "24.0.8215888" // needs use 24.0.8215888 or higher on MacOS 26.+ to prevent linking error
    } else {
        ndkVersion = "21.3.6528147"
    }

    defaultConfig {
        if (osName.contains('mac')) {
            minSdk 19 // ndk 24.0.8215888 only support minSdk 19
        } else {
            minSdk 16
        }

        externalNativeBuild {
            cmake {
                arguments   "-DANDROID_STL=c++_shared",
                            "-DSKITY_EXAMPLE=OFF",
                            "-DSKITY_TEST=OFF",
                            "-DSKITY_LOG=ON",
                            "-DSKITY_CODEC_MODULE=OFF",
                            "-DSKITY_BUILD_INSTALL=OFF"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        cmake {
            path "../../CMakeLists.txt"
        }
    }

    buildFeatures {
        prefabPublishing true
    }

    prefab {
        skity {
            headers "../../include"
        }
    }

    packagingOptions {
        exclude("**/libc++_shared.so")
    }
}
